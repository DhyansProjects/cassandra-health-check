apply plugin: 'java'

version = '1.0.0-SNAPSHOT'
group = 'com.lookout.cassandra'
defaultTasks 'check', 'fatJar'

task fatJar(type: Jar) {
    manifest {
    attributes 'Implementation-Title': 'Cassandra Health Check',
        'Implementation-Version': version,
        'Main-Class': 'com.lookout.cassandra.CassandraHealthCheck'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    jcenter()
    maven {
        url 'https://dl.bintray.com/rkuris/cassandra-driver-core'
    }
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile (group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '2.0.11+JAVA-815') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile group: 'args4j', name: 'args4j', version: '2.32'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    testCompile (group: 'org.cassandraunit', name: 'cassandra-unit', version: '2.1.3.1') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'com.datastax.cassandra', module: 'cassandra-driver-core'
        exclude group: 'io.netty', module: 'netty-all'
    }
    testCompile group: 'io.netty', name: 'netty-all', version: '4.0.27.Final'
    testCompile group: 'com.google.collections', name: 'google-collections', version: '1.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
